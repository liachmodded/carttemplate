plugins {
    id 'fabric-loom' version '0.2.5-SNAPSHOT'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'net.minecrell.licenser' version '0.4.1'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

if (isNullOrEmpty(System.getenv("TRAVIS_TAG")) && !isNullOrEmpty(System.getenv('TRAVIS_BUILD_NUMBER'))) {
    version = project.modVersion + '.' + System.getenv('TRAVIS_BUILD_NUMBER')
} else {
    version = project.modVersion
}
logger.lifecycle("${project.archivesBaseName} version ${project.version}")
group = project.mavenGroup

minecraft {
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraftVersion}"
    mappings "net.fabricmc:yarn:${project.yarnMappings}"
    modCompile "net.fabricmc:fabric-loader:${project.loaderVersion}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    // modCompile "net.fabricmc.fabric-api:fabric-api:${project.apiVersion}"

    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.
}

sourceSets {
    code {
        compileClasspath += sourceSets.main.compileClasspath
        compileClasspath += configurations.minecraftNamed
        runtimeClasspath += sourceSets.main.runtimeClasspath
        compileClasspath += configurations.minecraftNamed
    }
    data {
        compileClasspath += sourceSets.code.compileClasspath
        compileClasspath += sourceSets.code.output
        runtimeClasspath += sourceSets.code.compileClasspath
        runtimeClasspath += sourceSets.code.output
    }
}

license {
    header = file('HEADER.txt')
    include '**/*.java'
    style.java = 'BLOCK_COMMENT'
    newLine = false
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.links(
            'https://google.github.io/guava/releases/21.0/api/docs/',
            'http://asm.ow2.org/asm50/javadoc/user/',
            'https://docs.oracle.com/javase/8/docs/api/',
            'http://jenkins.liteloader.com/job/Mixin/javadoc/',
            'https://logging.apache.org/log4j/2.x/log4j-api/apidocs/'
            // Need to add minecraft jd publication once there is one available
    )

    // Disable the crazy super-strict doclint tool in Java 8
    options.addStringOption('Xdoclint:none', '-quiet')
}

def dataDir = project.file(".gradle/datadump/")
def dataCacheDir = new File(dataDir, "/.cache/")

task deleteOldData(type: Delete) {
    delete dataDir
}

task generateData(type: JavaExec) {
    dependsOn deleteOldData

    doFirst {
        dataDir.mkdirs()
    }

    classpath = sourceSets.data.runtimeClasspath

    main = project.dataEntry

    // arguments to pass to the application

    workingDir dataDir
    args dataDir.getAbsolutePath()
}

processResources {
    dependsOn generateData

    from(dataDir) {
        include "/data/"
    }
    from sourceSets.code.resources.srcDirs

    inputs.properties "version": project.version, "description": project.description

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version, "description": project.description
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.code.allSource
}

task javadocs(type: Javadoc) {
    source = sourceSets.code.allJava
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = "javadocs"
    from javadocs.destinationDir
}

jar {
    dependsOn generateData
    from sourceSets.code.output
    from "LICENSE"
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocsJar
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : System.getenv('bintray_user')
    key = project.hasProperty('bintrayKey') ? project.getProperty('bintrayKey') : System.getenv('bintray_key')
    publish = true
    override = true
    publications = ['mavenJava']
    pkg {
        repo = project.bintrayRepo
        name = project.archivesBaseName
        user = bintray.user
        userOrg = project.repoOwner
        licenses = [project.codeLicense]
        desc = project.description
        websiteUrl = "https://github.com/${project.repoOwner}/${project.archivesBaseName}"
        issueTrackerUrl = "https://github.com/${project.repoOwner}/${project.archivesBaseName}/issues"
        vcsUrl = "https://github.com/${project.repoOwner}/${project.archivesBaseName}.git"
        publicDownloadNumbers = true

        githubRepo = "${project.repoOwner}/${project.archivesBaseName}"
        version {
            name = project.version
            released = new Date()
            if (!isNullOrEmpty(System.getenv("TRAVIS_TAG"))) {
                vcsTag = System.getenv("TRAVIS_TAG")
            } else {
                vcsTag = ''
            }
        }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(jar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
            artifact javadocsJar
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}

static boolean isNullOrEmpty(String st) {
    return st == null || st.isEmpty()
}
